!ifndef MURA_INSTALLER_REPAIR_HEAL_NSH
!define MURA_INSTALLER_REPAIR_HEAL_NSH

Var /GLOBAL MuraRegistryInstallIsValid
Var /GLOBAL MuraInnerFailureSummary
Var /GLOBAL MuraInnerRootCode
Var /GLOBAL MuraInnerFailureReadResult

!macro MURA_READ_LAST_INNER_FAILURE
  InitPluginsDir
  StrCpy $MuraInnerRootCode ""
  StrCpy $MuraInnerFailureSummary "No specific locking process was identified. Close Mura, terminals, editors, and file managers opened in the install folder."
  nsExec::ExecToStack `"$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" -NoProfile -ExecutionPolicy Bypass -Command "& { \
    $$ErrorActionPreference = 'SilentlyContinue'; \
    $$logPath = '$MuraSessionLogPath'; \
    $$summary = 'No specific locking process was identified. Close Mura, terminals, editors, and file managers opened in the install folder.'; \
    $$code = ''; \
    if ($$logPath -and (Test-Path -LiteralPath $$logPath)) { \
      $$events = @(Get-Content -LiteralPath $$logPath -ErrorAction SilentlyContinue | ForEach-Object { try { $$_ | ConvertFrom-Json } catch { $$null } } | Where-Object { $$_ }); \
      $$failure = @($$events | Where-Object { $$_.event -eq 'failure' -and $$_.updated -eq $$true } | Select-Object -Last 1)[0]; \
      if (-not $$failure) { $$failure = @($$events | Where-Object { $$_.event -eq 'failure' } | Select-Object -Last 1)[0] }; \
      if ($$failure) { \
        $$code = ([string]$$failure.code).Trim(); \
        $$phase = ([string]$$failure.phase).Trim(); \
        $$path = ([string]$$failure.failedPath).Trim(); \
        $$blocking = ''; \
        $$processes = @($$failure.blockingProcesses); \
        if ($$processes.Count -gt 0) { $$blocking = (@($$processes | ForEach-Object { if ($$_.pid) { [string]$$_.name + '(' + [string]$$_.pid + ')' } else { [string]$$_.name } }) -join ', ') }; \
        if (-not $$blocking) { $$blocking = ([string]$$failure.message).Trim() }; \
        if (-not $$blocking) { $$blocking = 'Windows did not identify a specific locking process. Close terminals, editors, and file managers opened in the install folder.' }; \
        $$parts = @('- Outer installer: previous uninstaller exited with code $R0', ('- Inner failure: ' + $$code + ' phase ' + $$phase)); \
        if ($$path) { $$parts += ('- File or folder: ' + $$path) }; \
        $$parts += ('- Blocking process: ' + $$blocking); \
        $$summary = $$parts -join [Environment]::NewLine; \
      } \
    }; \
    if (-not $$code) { $$code = '-----' }; \
    [Console]::Out.Write($$code + '|' + $$summary) \
  }"`
  Pop $MuraInnerFailureReadResult
  Pop $MuraInnerFailureReadResult
  StrCpy $MuraInnerRootCode $MuraInnerFailureReadResult 5
  ${If} $MuraInnerRootCode == "-----"
    StrCpy $MuraInnerRootCode ""
  ${EndIf}
  StrCpy $MuraInnerFailureSummary $MuraInnerFailureReadResult 4096 6
!macroend

!macro MURA_LOG_UNINSTALLER_REPAIR _PHASE
  nsExec::Exec `"$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" -NoProfile -ExecutionPolicy Bypass -Command "& { \
    $$ErrorActionPreference = 'SilentlyContinue'; \
    $$log = '$MuraSessionLogPath'; \
    if (-not $$log) { $$log = Join-Path $$env:TEMP '${MURA_FALLBACK_LOG}' }; \
    $$path = '$INSTDIR\${UNINSTALL_FILENAME}'; \
    $$item = Get-Item -LiteralPath $$path -ErrorAction SilentlyContinue; \
    $$version = if ($$item) { $$item.VersionInfo.ProductVersion } else { '' }; \
    $$length = if ($$item) { $$item.Length } else { '' }; \
    $$payload = [ordered]@{ schemaVersion = 1; ts = (Get-Date -Format o); session = '$MuraSessionId'; version = '${VERSION}'; arch = '${MURA_TARGET_ARCH}'; updated = ('$MuraIsUpdated' -eq '1'); instDir = '$INSTDIR'; event = 'uninstaller-repair'; phase = '${_PHASE}'; path = $$path; exists = [bool]$$item; productVersion = $$version; length = $$length }; \
    Add-Content -LiteralPath $$log -Encoding UTF8 -Value ($$payload | ConvertTo-Json -Compress -Depth 8) \
  }"`
  Pop $MuraRepairLogResult
!macroend

!macro MURA_REPAIR_INSTALLED_UNINSTALLER
  Var /GLOBAL MuraInstalledUninstaller
  Var /GLOBAL MuraBundledUninstaller
  Var /GLOBAL MuraRepairLogResult

  !insertmacro MURA_LOG_UNINSTALLER_REPAIR "before"
  StrCpy $MuraInstalledUninstaller "$INSTDIR\${UNINSTALL_FILENAME}"

  InitPluginsDir
  StrCpy $MuraBundledUninstaller "$PLUGINSDIR\Mura-fixed-uninstaller.exe"
  SetOverwrite on
  File "/oname=$PLUGINSDIR\Mura-fixed-uninstaller.exe" "${UNINSTALLER_OUT_FILE}"

  ${If} ${FileExists} "$MuraInstalledUninstaller"
    ClearErrors
    CopyFiles /SILENT "$MuraBundledUninstaller" "$MuraInstalledUninstaller"
    ${If} ${Errors}
      !insertmacro MURA_LOG_UNINSTALLER_REPAIR "copy-failed-retry"
      !insertmacro MURA_STOP_APP_PROCESSES
      Sleep 1000

      ClearErrors
      CopyFiles /SILENT "$MuraBundledUninstaller" "$MuraInstalledUninstaller"
      ${If} ${Errors}
        ${If} ${FileExists} "$MuraBundledUninstaller"
          !insertmacro MURA_LOG_UNINSTALLER_REPAIR "copy-failed-using-bundled"
          !insertmacro MURA_LOG_EVENT "event=uninstaller-repair phase=copy-failed-using-bundled"
        ${Else}
          !insertmacro MURA_FAIL_REPORTABLE_BILINGUAL ${MURA_E_UNINSTALLER_COPY_OR_REBUILD_FAILED} "uninstaller-repair copy-failed-retry-bundled-missing" "${MURA_MSG_UNINSTALLER_COPY_LOCKED_EN}" "${MURA_MSG_UNINSTALLER_COPY_LOCKED_ZH}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_EN}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_ZH}"
        ${EndIf}
      ${Else}
        !insertmacro MURA_LOG_UNINSTALLER_REPAIR "after-copy-retry"
      ${EndIf}
    ${Else}
      !insertmacro MURA_LOG_UNINSTALLER_REPAIR "after-copy"
    ${EndIf}
  ${Else}
    ClearErrors
    CopyFiles /SILENT "$MuraBundledUninstaller" "$MuraInstalledUninstaller"
    ${If} ${Errors}
      !insertmacro MURA_FAIL_REPORTABLE_BILINGUAL ${MURA_E_UNINSTALLER_COPY_OR_REBUILD_FAILED} "uninstaller-repair rebuild-failed" "${MURA_MSG_UNINSTALLER_REBUILD_FAILED_EN}" "${MURA_MSG_UNINSTALLER_REBUILD_FAILED_ZH}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_EN}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_ZH}"
    ${EndIf}

    ${IfNot} ${FileExists} "$MuraInstalledUninstaller"
      !insertmacro MURA_FAIL_REPORTABLE_BILINGUAL ${MURA_E_UNINSTALLER_COPY_OR_REBUILD_FAILED} "uninstaller-repair rebuild-missing-after-copy" "${MURA_MSG_UNINSTALLER_REBUILD_MISSING_EN}" "${MURA_MSG_UNINSTALLER_REBUILD_MISSING_ZH}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_EN}" "${MURA_MSG_UNINSTALLER_REPAIR_ACTION_ZH}"
    ${EndIf}

    !insertmacro MURA_LOG_UNINSTALLER_REPAIR "rebuilt"
    !insertmacro MURA_LOG_EVENT "event=uninstaller-repair phase=rebuilt"
  ${EndIf}
!macroend

!macro MURA_HEAL_INSTALL_REGISTRY
  Var /GLOBAL MuraRegInstallLocation
  Var /GLOBAL MuraRegUninstallString
  Var /GLOBAL MuraRegInstallExe

  StrCpy $MuraRegistryInstallIsValid "0"

  ReadRegStr $MuraRegInstallLocation SHCTX "${INSTALL_REGISTRY_KEY}" "InstallLocation"
  ReadRegStr $MuraRegUninstallString SHCTX "${UNINSTALL_REGISTRY_KEY}" "UninstallString"

  ${If} $MuraRegInstallLocation == ""
    !insertmacro MURA_LOG_EVENT "event=registry-heal phase=missing-install-location uninstallString=$MuraRegUninstallString"
    !insertmacro MURA_CLEAR_INSTALL_REGISTRY "missing-install-location"
  ${Else}
    StrCpy $MuraRegInstallExe "$MuraRegInstallLocation\${MURA_APP_EXECUTABLE_FILENAME}"
    ${If} ${FileExists} "$MuraRegInstallExe"
      StrCpy $INSTDIR "$MuraRegInstallLocation"
      StrCpy $MuraRegistryInstallIsValid "1"
      !insertmacro MURA_LOG_EVENT "event=registry-heal phase=valid-install-location instDir=$INSTDIR uninstallString=$MuraRegUninstallString"
    ${Else}
      !insertmacro MURA_LOG_EVENT "event=registry-heal phase=stale-install-location installLocation=$MuraRegInstallLocation uninstallString=$MuraRegUninstallString"
      !insertmacro MURA_CLEAR_INSTALL_REGISTRY "stale-install-location"
    ${EndIf}
  ${EndIf}
!macroend

!macro MURA_LOG_UNINSTALL_RESULT _ROOT_KEY _HAD_ERRORS
  nsExec::Exec `"$SYSDIR\WindowsPowerShell\v1.0\powershell.exe" -NoProfile -ExecutionPolicy Bypass -Command "& { \
    $$ErrorActionPreference = 'SilentlyContinue'; \
    $$log = '$MuraSessionLogPath'; \
    if (-not $$log) { $$log = Join-Path $$env:TEMP '${MURA_FALLBACK_LOG}' }; \
    $$payload = [ordered]@{ schemaVersion = 1; ts = (Get-Date -Format o); session = '$MuraSessionId'; version = '${VERSION}'; arch = '${MURA_TARGET_ARCH}'; updated = ('$MuraIsUpdated' -eq '1'); instDir = '$INSTDIR'; event = 'uninstall-result'; root = '${_ROOT_KEY}'; launchErrors = '${_HAD_ERRORS}'; exitCode = '$R0' }; \
    Add-Content -LiteralPath $$log -Encoding UTF8 -Value ($$payload | ConvertTo-Json -Compress -Depth 8) \
  }"`
  Pop $MuraUninstallLogResult
!macroend

!macro MURA_HANDLE_UNINSTALL_RESULT _ROOT_KEY _LABEL_PREFIX
  ${If} ${Errors}
    StrCpy $MuraUninstallHadErrors "1"
  ${Else}
    StrCpy $MuraUninstallHadErrors "0"
  ${EndIf}

  !insertmacro MURA_LOG_UNINSTALL_RESULT "${_ROOT_KEY}" "$MuraUninstallHadErrors"

  ${If} $MuraUninstallHadErrors == "1"
    DetailPrint `Uninstall was not successful. Not able to launch uninstaller!`
    Return
  ${EndIf}

  ${If} $R0 != 0
      DetailPrint `Uninstall was not successful. Uninstaller error code: $R0.`
      !insertmacro MURA_READ_LAST_INNER_FAILURE
      ${If} $MuraLockerList != ""
        StrCpy $MuraInnerFailureSummary "- Failure: previous uninstaller failed with exit code $R0$\r$\n- File or folder: $INSTDIR$\r$\n- Blocking process: $MuraLockerList"
      ${EndIf}
      !insertmacro MURA_LOG_EVENT "event=old-uninstaller-failed action=report exitCode=$R0 lockers=$MuraLockerList uninstallerDetail=$MuraInnerFailureSummary"
      ${If} $MuraInnerRootCode != ""
        !insertmacro MURA_FAIL_REPORTABLE_ROOTED_BILINGUAL_DIAGNOSTICS "$MuraInnerRootCode" ${MURA_E_OLD_UNINSTALL_FAILED} "old-uninstaller exitCode=$R0 lockers=$MuraLockerList uninstallerDetail=$MuraInnerFailureSummary" "${MURA_MSG_OLD_UNINSTALL_FAILED_EN}" "${MURA_MSG_OLD_UNINSTALL_FAILED_ZH}" "${MURA_MSG_OLD_UNINSTALL_ACTION_EN}" "${MURA_MSG_OLD_UNINSTALL_ACTION_ZH}" "$MuraInnerFailureSummary" "$MuraInnerFailureSummary"
      ${Else}
        !insertmacro MURA_FAIL_REPORTABLE_BILINGUAL_DIAGNOSTICS ${MURA_E_OLD_UNINSTALL_FAILED} "old-uninstaller exitCode=$R0 lockers=$MuraLockerList uninstallerDetail=$MuraInnerFailureSummary" "${MURA_MSG_OLD_UNINSTALL_FAILED_EN}" "${MURA_MSG_OLD_UNINSTALL_FAILED_ZH}" "${MURA_MSG_OLD_UNINSTALL_ACTION_EN}" "${MURA_MSG_OLD_UNINSTALL_ACTION_ZH}" "$MuraInnerFailureSummary" "$MuraInnerFailureSummary"
      ${EndIf}
  ${EndIf}
!macroend

!macro customInit
  !insertmacro MURA_HEAL_INSTALL_REGISTRY
  ${If} $MuraRegistryInstallIsValid == "1"
    !insertmacro MURA_REPAIR_INSTALLED_UNINSTALLER
  ${EndIf}
!macroend

!macro customUnInstallCheck
  !insertmacro MURA_HANDLE_UNINSTALL_RESULT "SHELL_CONTEXT" "shctx"
!macroend

!macro customUnInstallCheckCurrentUser
  !insertmacro MURA_HANDLE_UNINSTALL_RESULT "HKEY_CURRENT_USER" "hkcu"
!macroend

!endif
